// Gradle build script for the jme-vehicles project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    // module coordinates of external dependencies:
    garrettCoordinates = 'com.github.stephengold:Garrett:0.5.3'
    heartCoordinates = 'com.github.stephengold:Heart:8.6.0'
    jmePowerCoordinates = 'com.github.stephengold:JmePower:1.1.1'
    jmeTtfCoordinates = 'com.github.stephengold:jme-ttf:3.0.0'
    minieCoordinates = 'com.github.stephengold:Minie:7.6.0'
    skyControlCoordinates = 'com.github.stephengold:SkyControl:1.0.4'

    // current versions of libraries:
    jme3Version = '3.6.0-stable'
    maVehiclesVersion = '0.7.2-SNAPSHOT'
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of SNAPSHOTs
}

tasks.register('checkstyle') {
    dependsOn ':HelloMav:checkstyleMain', ':MavCommon:checkstyleMain', \
            ':MavDemo1:checkstyleMain', ':MavDemo2:checkstyleMain', \
            ':MavLibrary:checkstyleMain'
    description 'Checks the style of all Java sourcecode.'
}

// publishing tasks:

tasks.register('install') {
    dependsOn ':MavLibrary:install'
    description 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':MavLibrary:release'
    description 'Stages Maven artifacts to Sonatype OSSRH.'
}
