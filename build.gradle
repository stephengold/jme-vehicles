// Gradle script to build the More Advanced Vehicles project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    maVehiclesVersion = '0.7.3-SNAPSHOT'
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('checkstyle') {
    dependsOn ':HelloMav:checkstyleMain', ':MavCommon:checkstyleMain', \
            ':MavDemo1:checkstyleMain', ':MavDemo2:checkstyleMain', \
            ':MavLibrary:checkstyleMain'
    description = 'Checks the style of all Java sourcecode.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':MavLibrary:install'
    description = 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':MavLibrary:release'
    description = 'Stages Maven artifacts to the Central Publisher Portal.'
}
